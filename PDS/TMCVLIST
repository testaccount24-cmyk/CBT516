*********************************************************************
*                                                                   *
*  TMCVLIST - PROGRAM TO CREATE A LIST OF VOLUMES IN COLUMNS 1-6    *
*             WHICH IS MANUFACTURED FROM THE TMS TMC.               *
*                                                                   *
*     AUTHOR:   SAM GOLOB            DATE:  05/31/01                *
*                                                                   *
*     INPUTS:   TMCIN    -  THE TMS TMC DATASET                     *
*                                                                   *
*     OUTPUTS:  VOLLIST  -  OUTPUT FB-80 LIST OF VOLUMES            *
*               SYSPRINT -  PRINTS RUN STATISTICS                   *
*                                                                   *
*     CHANGE LOG:                                                   *
*                                                                   *
*                                                                   *
*                                                                   *
*********************************************************************
TMCVLIST CSECT
         YREGS
         USING *,R15
         B     EYECATCH
         DC    C'--- '            MAKE EYECATCHER VISIBLE
         DC    C'TMCVLIST - CREATE VOL LIST FROM TMC - '
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '
         DC    C'LEVEL 001 '
         DC    C'--- '            MAKE EYECATCHER VISIBLE
SAVEAREA DC    18F'0'
EYECATCH DS    0H             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          SAVE PARM POINTER
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         DROP  R15
* -----                       SET UP TWO BASE REGISTERS
         USING TMCVLIST,R12,R11
         LR    R12,R15
         LR    R11,R12
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LR    R1,R5          RESTORE PARM POINTER
         PRINT GEN
         OPEN  (TMCIN,(INPUT))
         OPEN  (VOLLIST,(OUTPUT))
         ZAP   INRCDS,=P'0'
         ZAP   OUTRCDS,=P'0'
LEER     GET   TMCIN,AREAIN
         AP    INRCDS,=P'1'
         CLC   AREAIVOL,=C'TMSCTL'        TMS HEADER?
         BE    LEER                       IGNORE IT
         CLI   AREAIVOL,X'FF'             DSNB RECORD?
         BE    LEER                       IGNORE IT
         MVC   AREAOUT,AREAOUT-1         CLEAR OUTPUT AREA
         MVC   AREAOVOL(6),AREAIVOL       MOVE VOLUME SERIAL
         MVC   AREAOUT+30(TMCINDL),TMCIND    INDICATE VOL IS FROM TMC
         PUT   VOLLIST,AREAOUT
         AP    OUTRCDS,=P'1'
         B     LEER
FIN      DS    0H
         OPEN  (SYSPRINT,OUTPUT)
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADER
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSG01
         MVC   OUTLINE+19(12),EDPAT
         ED    OUTLINE+19(12),INRCDS+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSG02
         MVC   OUTLINE+19(12),EDPAT
         ED    OUTLINE+19(12),OUTRCDS+2
         PUT   SYSPRINT,OUTLINE
         CLOSE TMCIN
         CLOSE VOLLIST
         CLOSE SYSPRINT
THEEND   DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=0
* ----- CONSTANTS ------ *
ONE      DC    F'1'
DECVALUE DS    CL8
EDPAT    DC    X'402020206B2020206B202120'
         DC    C' '
BLANKS   DC    CL133' '
OUTLINE  DS    CL133
HEADER   DC    CL133'1  TMCVLIST - TMC VOLUME NAME EXTRACT PROGRAM'
         DC    CL133' '
MSG01    DC    CL133'-    RECORDS  IN ='
         DC    CL133' '
MSG02    DC    CL133'     RECORDS OUT ='
         DC    CL133' '
TMCIND   DC    C'FROM THE TMC'
TMCINDL  EQU   *-TMCIND
INRCDS   DC    PL7'0'
OUTRCDS  DC    PL7'0'
         LTORG
TMCIN    DCB   DDNAME=TMCIN,MACRF=GM,EODAD=FIN,DSORG=PS,LRECL=340
VOLLIST  DCB   DDNAME=VOLLIST,MACRF=PM,DSORG=PS,LRECL=80
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133
         DC    CL8' '
AREAIN   DS    0CL340
AREAIVOL DS    CL6
AREAIRST DS    CL334
         DC    CL8' '
AREAOUT  DS    0CL80
AREAOVOL DS    CL6
         DC    CL74' '
         DC    CL8' '
         END   TMCVLIST
