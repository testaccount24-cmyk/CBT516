*********************************************************************
*                                                                   *
*  TMLIBAUD - PROGRAM TO MATCH-MERGE VOLUME-BASED DATA FROM THE     *
*             TMC WITH CORRESPONDING DATA FROM THE TAPE LIBRARY.    *
*                                                                   *
*             A  TOTAL  EXCEPTION REPORT IS PRODUCED, PLUS          *
*             FOUR REPORTS SHOWING THE SEPARATE EXCEPTION TYPES     *
*                                                                   *
*                                                                   *
*   AUTHOR:   SAM GOLOB            DATE:  06/01/01                  *
*                                                                   *
*   INPUTS:   TMSIN    -  VOLUME LIST CREATED FROM THE TMC          *
*             LIBIN    -  VOLUME LIST CREATED FROM THE TCDB         *
*                                                                   *
*   OUTPUTS:  SYSPRINT -  PRINTS TOTAL EXCEPTION REPORT             *
*             SYSPRNT1 -                                            *
*             SYSPRNT2 -                                            *
*             SYSPRNT3 -                                            *
*             SYSPRNT4 -                                            *
*                                                                   *
*                                                                   *
*   CHANGE LOG:                                                     *
*                                                                   *
*                                                                   *
*                                                                   *
*                                                                   *
*                                                                   *
*********************************************************************
TMLIBAUD CSECT
         YREGS
         USING *,R15
         B     EYECATCH
         DC    C'--- '            MAKE EYECATCHER VISIBLE
         DC    C'TMLIBAUD - EXCEPTION REPORT - TMS VERSUS TAPE LIB - '
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '
         DC    C'LEVEL 001 '
         DC    C'--- '            MAKE EYECATCHER VISIBLE
SAVEAREA DC    18F'0'
EYECATCH DS    0H             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          SAVE PARM POINTER
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         DROP  R15
* -----                       SET UP TWO BASE REGISTERS
         USING TMLIBAUD,R12,R11
         LR    R12,R15
         LR    R11,R12
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LR    R1,R5          RESTORE PARM POINTER
         PRINT GEN
***********************************************************************
INITPAGE DS    0H                   FORCE INITIAL PRINT OF HEADERS
         ZAP   LINECNT,=P'61'
         ZAP   LINECNT1,=P'61'
         ZAP   LINECNT2,=P'61'
         ZAP   LINECNT3,=P'61'
         ZAP   LINECNT4,=P'61'
         ZAP   PAGECNT,=P'0'
         ZAP   PAGECNT1,=P'0'
         ZAP   PAGECNT2,=P'0'
         ZAP   PAGECNT3,=P'0'
         ZAP   PAGECNT4,=P'0'
***********************************************************************
* GET SYSTEM DATE AND TIME                                            *
***********************************************************************
         TIME  DEC,DATETIME,LINKAGE=SYSTEM,DATETYPE=MMDDYYYY
         OI    DATE+4,X'0F'
         UNPK  DATED,DATE
         OI    TIME+3,X'0F'
         UNPK  TIMED,TIME
         MVC   HEADDATE(2),DATED
         MVI   HEADDATE+2,C'/'
         MVC   HEADDATE+3(2),DATED+2
         MVI   HEADDATE+5,C'/'
         MVC   HEADDATE+6(4),DATED+4
         MVC   HEADDATE+15(2),TIMED
         MVI   HEADDATE+17,C':'
         MVC   HEADDATE+18(2),TIMED+2
         MVI   HEADDATE+20,C':'
         MVC   HEADDATE+21(2),TIMED+4
         MVC   HEADER+97(23),HEADDATE
         MVC   HEADER1+95(23),HEADDATE
         MVC   HEADER2+95(23),HEADDATE
         MVC   HEADER3+95(23),HEADDATE
         MVC   HEADER4+95(23),HEADDATE
***********************************************************************
PROCESS  DS    0H                        BEGIN MATCH-MERGE
*                                        OPEN INPUTS AND PRINT
         OPEN  (TMSIN,(INPUT))
         OPEN  (LIBIN,(INPUT))
         OPEN  (SYSPRINT,OUTPUT)
         OPEN  (SYSPRNT1,OUTPUT)
         OPEN  (SYSPRNT2,OUTPUT)
         OPEN  (SYSPRNT3,OUTPUT)
         OPEN  (SYSPRNT4,OUTPUT)
*                                        START ALL THE HEADERS
         BAL   R9,DOHEADR
*
         BAL   R9,DOHEADR1
*
         BAL   R9,DOHEADR2
*
         BAL   R9,DOHEADR3
*
         BAL   R9,DOHEADR4
*
*                                        ZERO THE COUNTERS
         ZAP   INTMCC,=P'0'
         ZAP   INLIBC,=P'0'
         ZAP   EXCEPNO,=P'0'
         MVI   FLAG,X'00'                AND THE FLAG
         MVI   FLAGPS,X'00'                AND THE OTHER FLAG
         MVC   TMCLVOL,BLANKS            AND THE 'LAST VOLUME READ'
         MVC   LIBLVOL,BLANKS             STORAGE AREAS
         MVC   AREAIN,AREAIN-1           AND BLANK THE
         MVC   AREAJN(256),AREAJN-1            INPUT AREAS
         MVC   AREAJN+256(AREAJNL-256),AREAJN+255
*
INITREAD DS    0H                        INITIALIZATION OF FILES
         GET   TMSIN,AREAIN
*                                        IF EOF IT'LL GO TO FTN
         CLI   AREAITAP,C'L'             SIGNIFICANT RECORD?
         BNE   INITREAD                  NO - IGNORE IT
         OI    FLAG,X'01'                INDICATE TMC RECD GOTTEN
         AP    INTMCC,=P'1'              INCREMENT INPUT COUNTER
         GET   LIBIN,AREAJN
*                                        IF EOF IT'LL GO TO FVN
         OI    FLAG,X'02'                INDICATE LIB RECD GOTTEN
         AP    INLIBC,=P'1'              INCREMENT INPUT COUNTER
         MVC   TMCLVOL,AREAIVOL          MOVE VOLSER NAMES --
         MVC   LIBLVOL,AREAJVOL           - BLANK IF NO INPUT
*
MATMERGE DS    0H                        MATCH MERGE PROCESS
         MVI   FLAGPS,X'00'              CLEAR VOLUME STATUS FLAG
         TM    FLAG,X'30'                EOF ON BOTH FILES?
         BO    EOFS                      YES. FINISH UP.
         TM    FLAG,X'10'                EOF ON TMC VOL FILE?
         BO    LOWLIB                    YES. JUST READ FROM VOL FILE
         TM    FLAG,X'20'                EOF ON LIB VOL FILE?
         BO    LOWTMC                    YES. JUST READ FROM TMC FILE
         CLC   TMCLVOL,LIBLVOL           COMPARE WHICH VOL IS LOWER
         BH    LOWLIB                    PROCESS LIBRARY VOL LOWER
         BL    LOWTMC                    PROCESS TMC VOL LOWER
         B     INEQUAL                   PROCESS EQUAL
*
*------------------------------------------------------------------*
INEQUAL  DS    0H                      BOTH ARE EQUAL - PRODUCE AUDIT
*
* -----     TEST IF EACH INPUT SHOWS PRIVATE OR SCRATCH
*
         CLC   AREAIPOS,=C'PRIVATE'
         BNE   INETISCR
         OI    FLAGPS,X'01'              FLAG TMS STATUS AS PRIVATE
INETISCR DS    0H
         CLC   AREAIPOS,=C'SCRATCH'
         BNE   INETIGO
         OI    FLAGPS,X'02'              FLAG TMS STATUS AS SCRATCH
INETIGO  DS    0H
         CLI   AREAJPOS,C'P'
         BNE   INETJSCR
         OI    FLAGPS,X'10'              FLAG LIB STATUS AS PRIVATE
         NI    FLAGPS,X'FF'-X'20'
         B     INETJGO
INETJSCR DS    0H
         CLI   AREAJPOS,C'S'
         BNE   INETJGO
         OI    FLAGPS,X'20'              FLAG LIB STATUS AS SCRATCH
         NI    FLAGPS,X'FF'-X'10'
         B     INETJGO
*
* -----     PRODUCE OUTPUT ONLY IF EXCEPTIONAL CASE
*
INETJGO  DS    0H
         CLI   FLAGPS,X'00'              NOT MARKED P OR S?
         BE    INEQGANO                  CAN'T PROCESS
         TM    FLAGPS,X'11'              BOTH MARKED PRIVATE?
         BO    INEQGANO                  YES - GET 2 MORE RECORDS
         CLI   AREAJVOL,C'D'             IS IT A "DEV" TAPE?
         BE    ITSADEV                   YES. SPECIAL TREATMENT.
         TM    FLAGPS,X'22'              BOTH MARKED SCRATCH?
         BO    INEQGANO                  YES - GET 2 MORE RECORDS
         B     INETNODV                  NO - TEST FOR EXCEPTIONS.
ITSADEV  DS    0H
         TM    FLAGPS,X'10'              YES - PRIVATE IN LIBRARY?
         BO    INEQGANO                  YES - AS IT SHOULD BE
         TM    FLAGPS,X'20'              YES - SCRATCH IN LIBRARY?
         BZ    INEQGANO                  NO  - COUNT AS OK
         MVC   AREAOUT,AREAOUT-1         CLEAR OUTPUT AREA
         MVC   AREAOVOL(6),LIBLVOL        MOVE VOLUME SERIAL
         MVC   AREAOUT+30(DEVNOTPL),DEVNOTP    MOVE ERROR MESSAGE IN
         AP    LINECNT,=P'1'
         ST    R9,SAVER9
         BAL   R9,DOHEADR
         L     R9,SAVER9
         PUT   SYSPRINT,AREAOUT
         AP    LINECNT3,=P'1'
         ST    R9,SAVER9
         BAL   R9,DOHEADR3
         L     R9,SAVER9
         PUT   SYSPRNT3,AREAOUT
         AP    EXCEPNO,=P'1'             MARK AS EXCEPTION
         AP    DEVNPTMS,=P'1'       DEV NOT PRIVATE IN TMS - COUNTER
         B     INEQGANO                  AFTER ERRORMSG, GO ON
*
* -----     TEST WHICH EXCEPTION WE HAVE
*
INETNODV DS    0H               NOW TEST THE EXCEPTIONS, AND REPORT
         TM    FLAGPS,X'21'     TMS IS PRIVATE, LIB IS SCRATCH?
         BNO   INETTSLP         NO. TEST FOR OPPOSITE.
         BAL   R9,TSLP          YES. HANDLE EXCEPTION.
         B     INETNOPR
INETTSLP DS    0H
         TM    FLAGPS,X'12'     TMS IS SCRATCH, LIB IS PRIVATE?
         BNO   INETNOPR         NO. GO ON PROCESSING.
         BAL   R9,TPLS          YES. HANDLE EXCEPTION.
         B     INETNOPR
INETNOPR DS    0H
         B     INEQGANO         GET 2 NEW RECORDS
* - - - - -        IF RECORDS EQUAL GET TWO NEW RECORDS      - - - *
INEQGANO DS    0H
         GET   TMSIN,AREAIN
*                                        IF EOF, GO TO FTN
         OI    FLAG,X'01'                INDICATE TMC RECD GOTTEN
         CLI   AREAITAP,C'L'             SIGNIFICANT RECORD?
         BNE   INEQGANO                  NO - IGNORE IT
         AP    INTMCC,=P'1'              INCREMENT INPUT COUNTER
         MVC   TMCLVOL,AREAIVOL          MOVE TMC VOLSER TO AREA
* - - - - -
         GET   LIBIN,AREAJN
*                                        IF EOF, GO TO FVN
         OI    FLAG,X'02'                INDICATE LIB RECD GOTTEN
         AP    INLIBC,=P'1'              INCREMENT INPUT COUNTER
         MVC   LIBLVOL,AREAJVOL          MOVE LIB VOLSER TO AREA
* - - - - -
         B     MATMERGE                  GO LOOP
*------------------------------------------------------------------*
         SPACE 3
*
LOWTMC   DS    0H
         NI    FLAG,X'FF'-X'02'          TURN OFF LIB RECD AS LAST IN
* - - - - -        GET ANOTHER TMC VOLUME RECORD             - - - *
LOWTGANO DS    0H
         GET   TMSIN,AREAIN
*                                        IF EOF, GO TO FTN
         CLI   AREAITAP,C'L'             SIGNIFICANT RECORD?
         BNE   LOWTGANO                  NO - IGNORE IT
         OI    FLAG,X'01'                INDICATE TMC RECD GOTTEN
         AP    INTMCC,=P'1'              INCREMENT INPUT COUNTER
         MVC   TMCLVOL,AREAIVOL          MOVE TMC VOLSER TO AREA
* - - - - -
         B     MATMERGE                  GO LOOP
*------------------------------------------------------------------*
         SPACE 3
LOWLIB   DS    0H
         TM    FLAG,X'03'                BOTH RECORDS READ TOGETHER?
         BO    LOWNOERR                  NO ERROR - NO MESSAGE
         MVC   AREAOUT,AREAOUT-1         CLEAR OUTPUT AREA
         MVC   AREAOVOL(6),LIBLVOL        MOVE VOLUME SERIAL
         MVC   AREAOUT+30(VNOTTDL),VNOTTD    INDICATOR IN OUTPUT
         AP    LINECNT,=P'1'
         ST    R9,SAVER9
         BAL   R9,DOHEADR
         L     R9,SAVER9
         PUT   SYSPRINT,AREAOUT
         AP    LINECNT4,=P'1'
         ST    R9,SAVER9
         BAL   R9,DOHEADR4
         L     R9,SAVER9
         PUT   SYSPRNT4,AREAOUT
         AP    EXCEPNO,=P'1'             MARK AS EXCEPTION
         AP    VNTTDCNT,=P'1'
LOWNOERR DS    0H
         NI    FLAG,X'FF'-X'01'          TURN OFF TMC RECD AS LAST IN
* - - - - -        GET ANOTHER LIB VOLUME RECORD
         GET   LIBIN,AREAJN
*                                        IF EOF, GO TO FVN
         OI    FLAG,X'02'                INDICATE LIB RECD GOTTEN
         AP    INLIBC,=P'1'              INCREMENT INPUT COUNTER
         MVC   LIBLVOL,AREAJVOL          MOVE LIB VOLSER TO AREA
* - - - - -
         B     MATMERGE                  GO LOOP
*------------------------------------------------------------------*
         SPACE 3
FTN      DS    0H
         OI    FLAG,X'10'                 INDICATE EOF ON TMC VOL LIST
         MVC   TMCLVOL,=X'FFFFFFFFFFFF'    HIGH VALUES TO LAST NUMBER
         B     MATMERGE                   GO CHECK MAIN LOOP
FVN      DS    0H
         OI    FLAG,X'20'                 INDICATE EOF ON LIB VOL LIST
         MVC   LIBLVOL,=X'FFFFFFFFFFFF'    HIGH VALUES TO LAST NUMBER
         B     MATMERGE                   GO CHECK MAIN LOOP
*------------------------------------------------------------------*
EOFS     DS    0H
         CLOSE TMSIN
         CLOSE LIBIN
OUTPRT   DS    0H
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSGIT
         MVC   OUTLINE+35(12),EDPAT
         ED    OUTLINE+35(12),INTMCC+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSGIV
         MVC   OUTLINE+35(12),EDPAT
         ED    OUTLINE+35(12),INLIBC+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSGEXCEP
         MVC   OUTLINE+35(12),EDPAT
         ED    OUTLINE+35(12),EXCEPNO+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSGPRNSC
         MVC   OUTLINE+35(12),EDPAT
         ED    OUTLINE+35(12),PRNSCCNT+2
         MVI   OUTLINE,C'-'
         PUT   SYSPRINT,OUTLINE
         PUT   SYSPRNT1,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSGSCNPR
         MVC   OUTLINE+35(12),EDPAT
         ED    OUTLINE+35(12),SCNPRCNT+2
         PUT   SYSPRINT,OUTLINE
         MVI   OUTLINE,C'-'
         PUT   SYSPRNT2,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),DEVNPRVT
         MVC   OUTLINE+35(12),EDPAT
         ED    OUTLINE+35(12),DEVNPTMS+2
         PUT   SYSPRINT,OUTLINE
         MVI   OUTLINE,C'-'
         PUT   SYSPRNT3,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),VNTTPCTR
         MVC   OUTLINE+35(12),EDPAT
         ED    OUTLINE+35(12),VNTTDCNT+2
         PUT   SYSPRINT,OUTLINE
         MVI   OUTLINE,C'-'
         PUT   SYSPRNT4,OUTLINE
         CLOSE SYSPRINT
         CLOSE SYSPRNT1
         CLOSE SYSPRNT2
         CLOSE SYSPRNT3
         CLOSE SYSPRNT4
THEEND   DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=0
* ----- SUBROUTINES ------ *
TPLS     DS    0H
         MVC   AREAOUT,AREAOUT-1         CLEAR OUTPUT AREA
         MVC   AREAOVOL(6),TMCLVOL        MOVE VOLUME SERIAL
         MVC   AREAOUT+30(PRNOTSCL),PRNOTSC   INDICATOR IN OUTPUT
         AP    LINECNT,=P'1'
         ST    R9,SAVER9
         BAL   R9,DOHEADR
         L     R9,SAVER9
         PUT   SYSPRINT,AREAOUT
         AP    LINECNT1,=P'1'
         ST    R9,SAVER9
         BAL   R9,DOHEADR1
         L     R9,SAVER9
         PUT   SYSPRNT1,AREAOUT
         AP    EXCEPNO,=P'1'             MARK AS EXCEPTION
         AP    PRNSCCNT,=P'1'         COUNT PRV IN LIB, SCR IN TMS
         BR    R9
* ------------------------ *
TSLP     DS    0H
         MVC   AREAOUT,AREAOUT-1         CLEAR OUTPUT AREA
         MVC   AREAOVOL(6),LIBLVOL        MOVE VOLUME SERIAL
         MVC   AREAOUT+30(SCNOTPRL),SCNOTPR   INDICATOR IN OUTPUT
         AP    LINECNT,=P'1'
         ST    R9,SAVER9
         BAL   R9,DOHEADR
         L     R9,SAVER9
         PUT   SYSPRINT,AREAOUT
         AP    LINECNT2,=P'1'
         ST    R9,SAVER9
         BAL   R9,DOHEADR2
         L     R9,SAVER9
         PUT   SYSPRNT2,AREAOUT
         AP    EXCEPNO,=P'1'             MARK AS EXCEPTION
         AP    SCNPRCNT,=P'1'         COUNT SCR IN LIB, PRV IN TMS
         BR    R9
* ------------------------ *         MAIN HEADER
DOHEADR  DS    0H
         CP    LINECNT,=P'60'
         BNH   DOHEADE
         AP    PAGECNT,=P'1'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADER
         MVC   OUTLINE+119(12),EDPAT
         ED    OUTLINE+119(12),PAGECNT+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVI   OUTLINE,C'-'
         PUT   SYSPRINT,OUTLINE
         ZAP   LINECNT,=P'4'
DOHEADE  BR    R9
* ------------------------ *          HEADER  1
DOHEADR1 DS    0H
         CP    LINECNT1,=P'60'
         BNH   DOHEADE1
         AP    PAGECNT1,=P'1'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADER1
         MVC   OUTLINE+119(12),EDPAT
         ED    OUTLINE+119(12),PAGECNT1+2
         PUT   SYSPRNT1,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVI   OUTLINE,C'-'
         PUT   SYSPRNT1,OUTLINE
         ZAP   LINECNT1,=P'4'
DOHEADE1 BR    R9
* ------------------------ *          HEADER  2
DOHEADR2 DS    0H
         CP    LINECNT2,=P'60'
         BNH   DOHEADE2
         AP    PAGECNT2,=P'1'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADER2
         MVC   OUTLINE+119(12),EDPAT
         ED    OUTLINE+119(12),PAGECNT2+2
         PUT   SYSPRNT2,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVI   OUTLINE,C'-'
         PUT   SYSPRNT2,OUTLINE
         ZAP   LINECNT2,=P'4'
DOHEADE2 BR    R9
* ------------------------ *          HEADER  3
DOHEADR3 DS    0H
         CP    LINECNT3,=P'60'
         BNH   DOHEADE3
         AP    PAGECNT3,=P'1'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADER3
         MVC   OUTLINE+119(12),EDPAT
         ED    OUTLINE+119(12),PAGECNT3+2
         PUT   SYSPRNT3,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVI   OUTLINE,C'-'
         PUT   SYSPRNT3,OUTLINE
         ZAP   LINECNT3,=P'4'
DOHEADE3 BR    R9
* ------------------------ *          HEADER  4
DOHEADR4 DS    0H
         CP    LINECNT4,=P'60'
         BNH   DOHEADE4
         AP    PAGECNT4,=P'1'
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADER4
         MVC   OUTLINE+119(12),EDPAT
         ED    OUTLINE+119(12),PAGECNT4+2
         PUT   SYSPRNT4,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVI   OUTLINE,C'-'
         PUT   SYSPRNT4,OUTLINE
         ZAP   LINECNT4,=P'4'
DOHEADE4 BR    R9
* ----- CONSTANTS ------ *
ONE      DC    F'1'
FLAG     DC    X'00'
*              X'01'        INPUT IS FROM TMC VOLUME LIST
*              X'02'        INPUT IS FROM LIB VOLUME LIST
*              X'10'        END OF INPUT ON TMC LIST
*              X'20'        END OF INPUT ON LIB LIST
FLAGPS   DC    X'00'
*              X'01'        TMC SAYS THIS VOLUME IS PRIVATE
*              X'02'        TMC SAYS THIS VOLUME IS SCRATCH
*              X'10'        LIB SAYS THIS VOLUME IS PRIVATE
*              X'20'        LIB SAYS THIS VOLUME IS SCRATCH
TMCLVOL  DC    CL6' '       LAST VOLUME NAME READ FROM TMC LIST
LIBLVOL  DC    CL6' '       LAST VOLUME NAME READ FROM VOL LIST
DECVALUE DS    CL8
EDPAT    DC    X'402020206B2020206B202120'
         DC    C' '
BLANKS   DC    CL133' '
OUTLINE  DS    CL133
HEADER   DC    CL133'1  TMLIBAUD - AUDIT REPORT - TMS AND LIBRARY EXCEP-
               TIONS AND INCONSISTENCIES - ALL EXCEPTIONS'
HEADER1  DC    CL133'1  TMLIBAUD - AUDIT REPORT - TAPES THAT ARE PRIVAT-
               E IN THE VTS AND SCRATCH TO TMS'
HEADER2  DC    CL133'1  TMLIBAUD - AUDIT REPORT - TAPES THAT ARE SCRATC-
               H IN THE VTS AND PRIVATE TO TMS'
HEADER3  DC    CL133'1  TMLIBAUD - AUDIT REPORT - DEVELOPMENT TAPES THA-
               T ARE NOT PRIVATE IN THE VTS'
HEADER4  DC    CL133'1  TMLIBAUD - AUDIT REPORT - VTS TAPES THAT ARE NO-
               T IN TMS'
MSGIT    DC    CL133'-         TMC VOLUME RECORDS  IN ='
MSGIV    DC    CL133'          LIB VOLUME RECORDS  IN ='
MSGEXCEP DC    CL133'      TOTAL NUMBER OF EXCEPTIONS ='
MSGPRNSC DC    CL133'  PRIVATE IN LIB, SCRATCH TO TMS ='
MSGSCNPR DC    CL133'  SCRATCH IN LIB, PRIVATE TO TMS ='
DEVNPRVT DC    CL133'    DEV TAPES NOT PRIVATE IN VTS ='
VNTTPCTR DC    CL133'   VTS TAPES THAT ARE NOT IN TMS ='
         DC    CL133' '
VNOTTD   DC    C'IN LIBRARY AND NOT IN TMS'
VNOTTDL  EQU   *-VNOTTD
PRNOTSC  DC    C'PRIVATE IN LIBRARY AND SCRATCH TO TMS'
PRNOTSCL EQU   *-PRNOTSC
SCNOTPR  DC    C'SCRATCH IN LIBRARY AND PRIVATE TO TMS'
SCNOTPRL EQU   *-SCNOTPR
DEVNOTP  DC    C'DEVELOPMENT TAPE IS NOT PRIVATE IN THE VTS LIBRARY'
DEVNOTPL EQU   *-DEVNOTP
LINECNT  DC    PL7'0'
LINECNT1 DC    PL7'0'
LINECNT2 DC    PL7'0'
LINECNT3 DC    PL7'0'
LINECNT4 DC    PL7'0'
PAGECNT  DC    PL7'0'
PAGECNT1 DC    PL7'0'
PAGECNT2 DC    PL7'0'
PAGECNT3 DC    PL7'0'
PAGECNT4 DC    PL7'0'
INTMCC   DC    PL7'0'
INLIBC   DC    PL7'0'
EXCEPNO  DC    PL7'0'
PRNSCCNT DC    PL7'0'
SCNPRCNT DC    PL7'0'
DEVNPTMS DC    PL7'0'
VNTTDCNT DC    PL7'0'
SAVER9   DC    F'0'
         LTORG
TMSIN    DCB   DDNAME=TMSIN,MACRF=GM,EODAD=FTN,DSORG=PS,LRECL=133
LIBIN    DCB   DDNAME=LIBIN,MACRF=GM,EODAD=FVN,DSORG=PS,LRECL=385
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
SYSPRNT1 DCB   DDNAME=SYSPRNT1,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
SYSPRNT2 DCB   DDNAME=SYSPRNT2,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
SYSPRNT3 DCB   DDNAME=SYSPRNT3,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
SYSPRNT4 DCB   DDNAME=SYSPRNT4,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
         DC    CL8' '
AREAIN   DS    0CL133            TMS   INPUT AREA
         DS    CL1
AREAIVOL DS    CL6               VOLSER
         DS    CL106
AREAITAP DS    CL1               "L" IF TAPE VOLUME RECORD
         DS    CL4
AREAIPOS DS    CL7               PRIVATE OR SCRATCH
         DS    CL8
         DC    CL8' '            SEPARATOR
AREAJN   DS    0CL385            LIB   INPUT AREA
         DS    CL26
AREAJLIB DS    CL8
         DS    CL24
AREAJVOL DS    CL6               VOLSER
AREAJPOS DC    CL1' '            P - PRIVATE,   S - SCRATCH
         DS    CL320
AREAJNL  EQU   *-AREAJN
         DC    C'    '
AREAOUT  DS    0CL133            SYSPRINT AREA
AREAOCC  DS    CL1
AREAOVOL DS    CL6
         DC    CL126' '
         DC    CL8' '
TIMED    DS    CL7
DATED    DS    CL9
DATETIME DS    4F
         ORG   DATETIME
TIME     DS    CL4
         DS    CL4
DATE     DS    CL5
         DS    CL3
HEADDATE DS    CL23
         END   TMLIBAUD
